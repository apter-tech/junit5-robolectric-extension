import org.robolectric.gradle.AarDepsPlugin

plugins {
    alias(libs.plugins.kotlinJvm)
    alias(libs.plugins.kotlinxKover)
    alias(libs.plugins.detekt)
}

apply plugin: AarDepsPlugin

configurations.configureEach { configuration ->
    configuration.exclude(group: 'androidx.tracing', module: 'tracing')
    configuration.exclude(group: 'androidx.annotation', module: 'annotation-experimental')
}

kotlin {
    jvmToolchain(libs.versions.jvmToolchain.get().toInteger())
}

test {
    useJUnitPlatform()
    jvmArgs '-Djunit.platform.launcher.interceptors.enabled=true'
}

detekt {
    version libs.versions.detekt.get()
    autoCorrect true
}

dependencies {
    detektPlugins(libs.detektFormatting)
    detektPlugins(libs.detektRulesLibraries)
    implementation libs.robolectric
    implementation libs.junit4
    implementation platform(libs.junit5Bom)
    implementation libs.bundles.junit5
    implementation(libs.guavaConstraint) {
        because 'CVE-2023-2976 7.1 Transitive Files or Directories Accessible to External Parties vulnerability ' +
                'with High severity found'
    }
    testImplementation libs.robolectricAndroidAll
    testImplementation libs.junit4
    testImplementation libs.androidxTestExtJunit
    testImplementation libs.kotlinTestJUnit5
    testImplementation libs.junit5JupiterParams
    testRuntimeOnly libs.junit5JupiterEngine
}
