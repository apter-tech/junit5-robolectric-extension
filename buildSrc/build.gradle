plugins {
    id('groovy')
}

repositories {
    google()
    mavenCentral()
    gradlePluginPortal()
}

dependencies {
    compileOnly(gradleApi())
    compileOnly(localGroovy())
    implementation(libs.guava)
    implementation(libs.androidGradle)
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(libs.versions.jvmToolchain.get()))
    }
}

task downloadAarDepsPlugin {
    final from = "https://raw.githubusercontent.com/robolectric/robolectric/robolectric-${libs.versions.robolectric.get()}/buildSrc/src/main/groovy/org/robolectric/gradle/AarDepsPlugin.java"
    final groovySourceSet = sourceSets.findByName('main').allSource.sourceDirectories.find { it.name == 'groovy' } as File
    final to = new File(groovySourceSet, '/org/robolectric/gradle/AarDepsPlugin.java')

    inputs.property("from", from)
    outputs.file(to)

    doLast {
        try {
            new URL(from).withInputStream { i -> to.withOutputStream { it << i } }
        } catch (IOException e) {
            logger.debug("Error during downloading AarDepsPlugin. Keep the stored version.\n$e")
        }
    }
}

tasks {
    compileJava.dependsOn(downloadAarDepsPlugin)
    compileGroovy.dependsOn(downloadAarDepsPlugin)
}
